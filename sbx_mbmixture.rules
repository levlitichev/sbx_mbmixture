from pathlib import Path

CHROMOSOMES=[i for i in range(1,20)]
IMP_SNP_DIR=Path(Cfg['sbx_mbmixture']['imp_snp_fp'])
# N.B. Can use a forward slash (`/`) in paths because QC_FP and IMP_SNP_FP are pathlib.Path objects
 
rule all_mbmixture_sample_results:
   input:
        expand(
            str(QC_FP/'mbmixture'/'{host}'/'summaries'/'sample_results'/'{sample}'/'all.rds'),
            sample=Samples.keys(), host=HostGenomes.keys())

rule all_mbmixture_pair_results:
   input:
        expand(
            str(QC_FP/'mbmixture'/'{host}'/'summaries'/'pair_results'/'{sample}'/'all.rds'),
            sample=Samples.keys(), host=HostGenomes.keys())

rule sort_and_index_host_reads_bam:
    input:
        str(QC_FP/'decontam'/'intermediates'/'{host}'/'{sample}.bam')
    output:
        str(QC_FP/'mbmixture'/'{host}'/'sorted'/'{sample}.bam')
    threads:
        Cfg['sbx_mbmixture']['threads']
    resources:
        mem_mb=16384 #16GB
    shell:
        "samtools sort -@ {threads} -O bam {input} > {output} && samtools index -@ {threads} {output}"

rule get_readcounts:
    input:
        str(QC_FP/'mbmixture'/'{host}'/'sorted'/'{sample}.bam')
    output:
        str(QC_FP/'mbmixture'/'{host}'/'readcounts'/'{sample}'/'chr{chr}.csv.gz')
    params:
        chr="{chr}"
    resources:
        mem_mb=16384 #16GB
    envmodules:
        "R/4.0.2"
    script:
        "scripts/get_readcounts_one_chr.R"


rule sample_summary:
    input:
        readcounts_path=str(QC_FP/'mbmixture'/'{host}'/'readcounts'/'{sample}'/'chr{chr}.csv.gz'),
        imp_snp_path=str(IMP_SNP_DIR/'chr{chr}.csv.gz')
    output:
        str(QC_FP/'mbmixture'/'{host}'/'summaries'/'sample_results'/'{sample}'/'chr{chr}.rds')
    params:
        sample="{sample}",
        chr="{chr}"
    threads:
        Cfg["sbx_mbmixture"]["threads"]
    resources:
        mem_mb=65536 #64GB
    envmodules:
        "R/4.0.2"
    script:
        "scripts/sample_summary.R"

rule pair_summary:
    input:
        readcounts_path=str(QC_FP/'mbmixture'/'{host}'/'readcounts'/'{sample}'/'chr{chr}.csv.gz'),
        imp_snp_path=str(IMP_SNP_DIR/'chr{chr}.csv.gz')
    output:
        str(QC_FP/'mbmixture'/'{host}'/'summaries'/'pair_results'/'{sample}'/'chr{chr}.rds')
    params:
        sample="{sample}",
        chr="{chr}"
    threads:
        Cfg["sbx_mbmixture"]["threads"]
    resources:
        mem_mb=65536 #64GB
    envmodules:
        "R/4.0.2"
    script:
        "scripts/pair_summary.R"

rule combine_sample_results:
    input:
        expand(
            str(QC_FP/'mbmixture'/'{host}'/'summaries'/'sample_results'/'{sample}'/'chr{chr}.rds'),
            chr=CHROMOSOMES, allow_missing=True)
    output:
        str(QC_FP/'mbmixture'/'{host}'/'summaries'/'sample_results'/'{sample}'/'all.rds')
    envmodules:
        "R/4.0.2"
    script:
        "scripts/combine_sample_results.R"

rule combine_pair_results:
    input:
        expand(
            str(QC_FP/'mbmixture'/'{host}'/'summaries'/'pair_results'/'{pair}'/'chr{chr}.rds'),
            chr=CHROMOSOMES, allow_missing=True)
    output:
        str(QC_FP/'mbmixture'/'{host}'/'summaries'/'pair_results'/'{pair}'/'all.rds')
    envmodules:
        "R/4.0.2"
    script:
        "scripts/combine_pair_results.R"
