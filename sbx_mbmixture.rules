from pathlib import Path

CHROMOSOMES=[i for i in range(1,20)]
IMP_SNP_DIR=Path(Cfg['sbx_mbmixture']['imp_snp_fp'])
# N.B. Can use a forward slash (`/`) in paths because QC_FP and IMP_SNP_FP are pathlib.Path objects
 
rule all_mbmixture:
   input:
        expand(
            str(QC_FP/'mbmixture'/'{host}'/'summaries'/'{sample}'/'pair_results_allchr.rds'),
            sample=Samples.keys(), host=HostGenomes.keys())

rule sort_and_index_host_reads_bam:
    input:
        str(QC_FP/'decontam'/'intermediates'/'{host}'/'{sample}.bam')
    output:
        str(QC_FP/'mbmixture'/'{host}'/'sorted'/'{sample}.bam')
    threads:
        Cfg['sbx_mbmixture']['threads']
    resources:
        mem_mb=24576 #24GB
    shell:
        "samtools sort -@ {threads} -O bam {input} > {output} && samtools index -@ {threads} {output}"

# TEMPORARY
rule all_get_readcounts:
    input:
        expand(
            str(QC_FP/'mbmixture'/'{host}'/'readcounts'/'{sample}'/'chr{chr}.csv.gz'),
            sample=Samples.keys(), host=HostGenomes.keys(), chr=CHROMOSOMES)

rule get_readcounts:
    input:
        str(QC_FP/'mbmixture'/'{host}'/'sorted'/'{sample}.bam')
    output:
        str(QC_FP/'mbmixture'/'{host}'/'readcounts'/'{sample}'/'chr{chr}.csv.gz')
    params:
        chr="{chr}"
    resources:
        mem_mb=16384 #16GB
    envmodules:
        "R/4.0.2"
    script:
        "scripts/get_readcounts_one_chr.R"

# TEMPORARY
rule all_compare_readcounts:
   input:
        expand(
            str(QC_FP/'mbmixture'/'{host}'/'summaries'/'pair_results'/'{sample}'/'chr{chr}.rds'),
            sample=Samples.keys(), host=HostGenomes.keys(), chr=CHROMOSOMES)

rule compare_readcounts:
    input:
        readcounts_path=str(QC_FP/'mbmixture'/'{host}'/'readcounts'/'{sample}'/'chr{chr}.csv.gz'),
        imp_snp_path=str(IMP_SNP_DIR/'chr{chr}.csv.gz')
    output:
        sample_results_out_path=str(
            QC_FP/'mbmixture'/'{host}'/'summaries'/'sample_results'/'{sample}'/'chr{chr}.rds'),
        pair_results_out_path=str(
            QC_FP/'mbmixture'/'{host}'/'summaries'/'pair_results'/'{sample}'/'chr{chr}.rds')
    params:
        sample="{sample}",
        chr="{chr}"
    resources:
        mem_mb=65536 #64GB
    envmodules:
        "R/4.0.2"
    script:
        "scripts/compare_readcounts_to_imp_snps_one_chr.R"

rule combine_results_across_chr:
    input:
        sample_results=expand(
            str(QC_FP/'mbmixture'/'{host}'/'summaries'/'{sample}'/'sample_results_chr{chr}.rds'),
            chr=CHROMOSOMES, allow_missing=True),
        pair_results=expand(
            str(QC_FP/'mbmixture'/'{host}'/'summaries'/'{sample}'/'pair_results_chr{chr}.rds'),
            chr=CHROMOSOMES, allow_missing=True)
    output:
        sample_results=str(QC_FP/'mbmixture'/'{host}'/'summaries'/'{sample}'/'sample_results_allchr.rds'),
        pair_results=str(QC_FP/'mbmixture'/'{host}'/'summaries'/'{sample}'/'pair_results_allchr.rds')
    envmodules:
        "R/4.0.2"
    script:
        "scripts/combine_results_across_chr.R"
